# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=calamares_config_ce
pkgver=22.03.1.17
pkgrel=1
_repo_name=EndeavourOS-calamares
pkgdesc='EndeavourOS calamares configuration files for Community Editions'
arch=('any')
url='https://www.endeavouros.com'
license=('GPL3')
source=("https://github.com/endeavouros-team/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('50c915742d745f8d9fe4744c6ccfbf37601835109b43bf79b8cec6030328c01046d48f910b7d97be74c4d6b20b5bcb87bd1ea91eca6ad18608ac594885245a94')


_caldir="$pkgname/calamares"

prepare() {
    mkdir -p "$_caldir/modules"
    mv "$_repo_name-$pkgver"            "$_repo_name"

    cp "${_repo_name}/calamares/modules/netinstall-ce-base.yaml"            "$_caldir/modules/"
    cp "${_repo_name}/calamares/modules/packagechooser_ce.conf"             "$_caldir/modules/"
    cp "${_repo_name}/calamares/modules/netinstall_community_base.conf"     "$_caldir/modules/"
    cp "${_repo_name}/calamares/settings_community.conf"                    "$_caldir/"
    cp -R "${_repo_name}/calamares/images-ce"                               "$_caldir/"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$_caldir" "$pkgdir/etc/"
}
