# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=calamares_config_default
pkgver=22.09.3.3
pkgrel=2
_repo_name=EndeavourOS-calamares
pkgdesc='EndeavourOS calamares configuration files and branding'
arch=('any')
url='https://www.endeavouros.com'
license=('GPL3')
source=("https://github.com/endeavouros-team/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('44ea6f8c71b6fe04cd54c79a5931fbf5ed83bec0a29d2b42f20d0ffa62baa2f8d315e96d1562cd0ff9bbc1c4eb8d7d4757acc21024e90abe197864a0c3c686cc')


_date=$(date +%Y.%m.%d)

prepare() {
    # set date for calamares_branding
    sed -i "$srcdir/${_repo_name}-${pkgver}/calamares/branding/endeavouros/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/${_repo_name}-${pkgver}/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}

    rm "$srcdir/${_repo_name}-${pkgver}/calamares/modules/netinstall-ce-base.yaml"
    rm "$srcdir/${_repo_name}-${pkgver}/calamares/settings_community.conf"
    rm "$srcdir/${_repo_name}-${pkgver}/calamares/modules/"{packagechooser_ce.conf,netinstall_community_base.conf}
    rm -R "$srcdir/${_repo_name}-${pkgver}/calamares/images-ce"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/${_repo_name}-${pkgver}/calamares" "$pkgdir/etc/"
}
