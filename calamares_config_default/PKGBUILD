# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=calamares_config_default
pkgver=22.07.2.1
pkgrel=2
_repo_name=EndeavourOS-calamares
pkgdesc='EndeavourOS calamares configuration files and branding'
arch=('any')
url='https://www.endeavouros.com'
license=('GPL3')
source=("https://github.com/endeavouros-team/$_repo_name/archive/refs/tags/$pkgver.tar.gz")

sha512sums=('91765abd3a4750683c5837dd52b3086d15d50aa3f1523300d61a0f1985420e629e75ffbd8c3ae35ef54d93570c910885a36befad57a6f324512819638bacbe04')


_date=$(date +%Y.%m.%d)

prepare() {
    # set date for calamares_branding
    sed -i "$srcdir/$_repo_name-$pkgver/calamares/branding/endeavouros/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/$_repo_name-$pkgver/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}

    rm "$srcdir/$_repo_name-$pkgver/calamares/modules/netinstall-ce-base.yaml"
    rm "$srcdir/$_repo_name-$pkgver/calamares/settings_community.conf"
    rm "$srcdir/$_repo_name-$pkgver/calamares/modules/"{packagechooser_ce.conf,netinstall_community_base.conf}
    rm -R "$srcdir/$_repo_name-$pkgver/calamares/images-ce"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/$_repo_name-$pkgver/calamares" "$pkgdir/etc/"
}
