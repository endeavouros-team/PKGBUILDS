# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=calamares_config_ce-git
pkgver=532.63c9016
pkgrel=1
_repo_name=EndeavourOS-calamares
pkgdesc='EndeavourOS calamares configuration files for Community Editions'
arch=('any')
url='https://www.endeavouros.com'
license=('GPL3')
source=("${_repo_name}::git+https://github.com/endeavouros-team/$_repo_name.git#branch=main")

sha512sums=('SKIP')


_caldir="$pkgname/calamares"

pkgver() {
  cd "$_repo_name"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p "$_caldir/modules"
    # mv "$_repo_name-$pkgver"            "$_repo_name"

    cp "${_repo_name}/calamares/modules/netinstall-ce-base.yaml"            "$_caldir/modules/"
    cp "${_repo_name}/calamares/modules/packagechooser_ce.conf"             "$_caldir/modules/"
    cp "${_repo_name}/calamares/modules/netinstall_community_base.conf"     "$_caldir/modules/"
    cp "${_repo_name}/calamares/settings_community.conf"                    "$_caldir/"
    cp -R "${_repo_name}/calamares/images-ce"                               "$_caldir/"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$_caldir" "$pkgdir/etc/"
}
