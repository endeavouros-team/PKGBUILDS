#!/bin/bash

# Maintainer: manuel at endeavouros.com

pkgname=fetch-iso
pkgdesc="A special helper tool for the EndeavourOS ISO testers"
url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
pkgver=25.11
pkgrel=1

arch=('any')
license=('GPL')
depends=(fzf)
optdepends=(
    'wget: alternative fetcher'
    'curl: alternative fetcher'
)
source=(
    $url/$pkgname
    $url/$pkgname.conf
    $url/$pkgname.completion
    $url/$pkgname-show-all-info
)
sha512sums=('4e8bfa7582a8a0f3519155620b0d0f08b63787020b1a576f9cdc0c92a5caeec9e07d4f08e6798668253d587194c1914b5a26ee6e556454c35e0363f70bef6bd7'
            '5b2897dc51353b33446ddefdae6d312b36da825ac505418aea535eacaeef568b1f024db171668878ed499e532bc0dc654d35633a8a9fe6c77e25b43e7288cfca'
            'd99fa711c27f4101dbe982ad69b383fe4dc4ebcb96c838155e0750d34f72a37d9586adf095963b13aa265a3cec3d211000e999da8ac1abd3076128b66d0776d6'
            '8a910e558e460e48e3c2d50bf8d3f1ccd17f4c8fee564e867105937c21806d3e62733611f01effbd8cdcfdf2e03b5852cfe470b21625df518fcbf04cadd9cf9f')
# backup=(etc/skel/$pkgname.conf)

package() {
    local xx name
    for xx in "${source[@]}" ; do
        name=$(basename "$xx")
        case "$name" in
            *.completion) install -Dm644 $name   $pkgdir/usr/share/bash-completion/completions/${name%.*} ;;
            *.conf)       install -Dm644 $name   $pkgdir/etc/skel/$name ;;
            *)            install -Dm755 $name   $pkgdir/usr/bin/$name ;;
        esac
    done
}
