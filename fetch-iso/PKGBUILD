#!/bin/bash

# Maintainer: manuel at endeavouros.com

pkgname=fetch-iso
pkgdesc="A special helper tool for the EndeavourOS ISO testers"
url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
pkgver=25.9
pkgrel=1

arch=('any')
license=('GPL')
depends=(fzf)
optdepends=(
    'wget: alternative fetcher'
    'curl: alternative fetcher'
)
source=(
    $url/$pkgname
    $url/$pkgname.conf
    $url/$pkgname.completion
    $url/$pkgname-show-all-info
)
sha512sums=('9b3af8a42e0940dae46ce66989ca5d7ca8b0e7529abcf0d57325e9ace7189c7da476dc6b94219467ac7aa97857de7ade8e5f32b773036223f2a89dba7f845c30'
            '5b2897dc51353b33446ddefdae6d312b36da825ac505418aea535eacaeef568b1f024db171668878ed499e532bc0dc654d35633a8a9fe6c77e25b43e7288cfca'
            'd99fa711c27f4101dbe982ad69b383fe4dc4ebcb96c838155e0750d34f72a37d9586adf095963b13aa265a3cec3d211000e999da8ac1abd3076128b66d0776d6'
            '25f721a1a103d4a3d7779d64027e7f6a763ff02fe1a512401bd939e734199f6a5d3d7a7553369dd856a07b57a1d6dc2bed1f7eed385dcba0b244dc7326616330')
# backup=(etc/skel/$pkgname.conf)

package() {
    local xx name
    for xx in "${source[@]}" ; do
        name=$(basename "$xx")
        case "$name" in
            *.completion) install -Dm644 $name   $pkgdir/usr/share/bash-completion/completions/${name%.*} ;;
            *.conf)       install -Dm644 $name   $pkgdir/etc/skel/$name ;;
            *)            install -Dm755 $name   $pkgdir/usr/bin/$name ;;
        esac
    done
}
