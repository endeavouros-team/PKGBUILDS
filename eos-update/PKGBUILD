# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-update
pkgdesc="A package updater with additional features for EndeavourOS"
pkgver=26.1.1
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  eos-translations
  expac
)
optdepends=(
    "yay: alternative for AUR updates in eos-update"
    "paru: alternative for AUR updates in eos-update"
    "arch-audit: for showing only packages that have already been fixed"
)

backup=(etc/eos-update-other.bash)

url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/refs/heads/master/$pkgname"


source=(
  $_url/eos-update
  $_url/eos-update.desktop
  $_url/eos-update.completion
  $_url/eos-update-descriptions
  $_url/eos-update-extras
  $_url/eos-update-fast
  $_url/eos-update-other.bash    # "config" for additional update tools
)

sha512sums=('e5919a100428d64e1fce1f755a6da1ff50eabaf6b82a7851395b554cbdfb06e156e07af26b51fa815cfff24b8dddcb7db3e620ff326fe18a236935e1e0879bdb'
            'c3a60ac60d50573d4d51aea7f4015f48f0f1740e5b195040131cb27edcd59baefc9c131128a22045de950b669d007917035f67b9b51dc7c34f71b4324816b6d2'
            '2e318929230800e4c75c7793811f856fc9cbaeadbadebb51ba852a10a2450dee1db1ee1071cee82bedc465f5f32646f395616f003bbb0e612270a43cd31a37f9'
            'fa0e76f45623a30995cd0b34b1c07390d1ba88b87c9304041ba12b2ca1d11fc90d19245bd24f8588d4987315bff96dd7e96e372384c03ccfe3a0bba9a3067fd6'
            '8f74252ab514d84b4ac57586838ac9582b1a14927518a3844e5b033a8b02de3e687af1e5e5e1fa13cd8b592d67847dcdb9936b85b530894ef0588fb088d2ccf3'
            '25d45e2698f4cfc46ba4f1b68053180a9292997eda7d8dbf15ab5370e9f8504634a37732076bdebfb5b7b1f109e8325a0662768cf8858f777fde8885559f7767'
            '72ef6a17db36d3b54cfa6396ce5dadc78c384f87625c15e53ea17a7606f693ce034568199c4863898d62a10a435b871af3371f4cea06a0bff356c9024ea8d7df')

package() {
  local file src

  for src in "${source[@]}" ; do
      file="${src##*/}"
      case "$file" in
          *.desktop)             install -Dm644 "$file"   "$pkgdir/usr/share/applications/$file" ;;
          *.completion)          install -Dm644 "$file"   "$pkgdir/usr/share/bash-completion/completions/${file%.completion}" ;;
          *.bash)                install -Dm644 "$file"   "$pkgdir/etc/$file" ;;
          *)                     install -Dm755 "$file"   "$pkgdir/usr/bin/$file" ;;
      esac
  done
}
