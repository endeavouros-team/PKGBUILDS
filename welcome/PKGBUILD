# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EndeavourOS users."
pkgver=25.12.1
pkgrel=1
arch=('any')
license=('GPL')
depends=(
    systemd eos-bash-shared eos-translations
    eos-qogir-icons
    yad
)
optdepends=(
  'translate-shell: for generating a language translation for the User Interface'
  'reflector-simple: for the Update Mirrors button'
  'kdiff3: alternative app for pacdiff to use'
  'meld: alternative app for pacdiff to use'
  'diffuse: alternative app for pacdiff to use'
  'vim: alternative app for pacdiff to use'
  'eos-quickstart: quick start with installing apps'
)

# url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
# _url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"
# https://github.com/endeavouros-team/welcome/raw/main/LICENSE
# https://raw.githubusercontent.com/endeavouros-team/welcome/main/LICENSE
url=https://github.com/endeavouros-team/welcome
_url=https://raw.githubusercontent.com/endeavouros-team/welcome/main

source=(
  $_url/$pkgname
  $_url/${pkgname}-installer.conf
  $_url/$pkgname.bash-completion
  $_url/$pkgname.desktop
  $_url/wallpaper-once
  $_url/wallpaper-once.desktop
  $_url/eos-kill-yad-zombies
  $_url/welcome-dnd
  $_url/welcome-tip-pages
  $_url/eos-install-mode-run-calamares
  $_url/arm-eos-welcome-installer
)
sha512sums=('521e81d38203853eff2cd899d9ff04a39b6b98d98fa2622750f0b47c53b8ef00a434350f9132c6df0ca8928a9d5dd97b1929db02e225bf388b5465957d62a56b'
            '63f8cb7eff80f8f3b69c1629c7acf7b3e5207cdcbd560060177ce1a8dbd7593ec1c42582787fc3446d429b52e7791145c815bc1f01f4c1a3a70b518bbb22cd18'
            '36dcdd5db1d86b88a87c4e48b718b0986107980fc289dd8dd643a365508255a81680d197442dfe6933bbb8b06e2ccc7d7b55bf28ce1a925cdfd0278c6fee2c7f'
            '5ca4b7108573a980d9d4011c5afde4f459abe8e36f5b6bc846368dcc48cfa2e672133f074fdeac4282835fa5b87f7afb2cd9dc3e985b4d2e8f574ac239c90ba2'
            'acd528bafe16d61690d532961da562b0e9238153aa609c7d84d719a19525afe191b9cd54294b837d1d81c4d6cc6c9ae766712174aecb3708314e869addc6b537'
            'a193a605d95d837db568dab2feb074c035bea12bc7b08a39a7be3b075dd063cd05fb46b4bde5c86f81a26456c0aab3b0b834cb1ffd88820095a14291b4e059a5'
            '90dfc1f08a428787305d72a2e439a377aee0bc095fcb0a2efb4245a15285d4b96b5e0ac644c02f27ab68aebbfc5c64e6ab7d733d9db8952d2999d589410d3b9e'
            '9f0cd5edabf93439656046c60dc6a29f439a9d8b7afab8ec03b9470321ba98fc53b927944e2f68b5b152c150cca76913490eaab4256a5699da5960f860d81e81'
            '42e5aeb0ea71e2d77fd6ad741a48bf3927a20a2a71c48d68aafbe462b32755bf3e40ac2d0a54471a15b0c6242cdbbc332fd095822609827b69c75371fb4109e0'
            '79bdace9a4ee8c1769ed0b822372b687b9098295d185388f26c37a4cdebc1d74a3d02919cb5868b219edced61ad5592d5dca92d0bc67879c4dc1e1398182ff15'
            'cbc39b4806cbfc6a81ddad7cc174d3850f82699c46c949a3ac9855f3c63f6cd6e91102ca357dc4176c7edcb02682e10b0175a95adcede81ec23a7ee84ab231aa')
package() {
    local -r eosdir=usr/share/endeavouros/scripts
    local src name

    for src in "${source[@]}" ; do
        name=${src##*/}
        case "$name" in
            *.bash-completion)   install -Dm644 "$name"  "$pkgdir/usr/share/bash-completion/completions/${name%.*}" ;;
            *.desktop)           install -Dm644 "$name"  "$pkgdir/etc/xdg/autostart/$name" ;;
            *.conf)              install -Dm644 "$name"  "$pkgdir/etc/$name" ;;
            "$pkgname")          install -Dm755 "$name"  "$pkgdir/$eosdir/$name" ;;
            wallpaper-once)      install -Dm755 "$name"  "$pkgdir/$eosdir/$name" ;;
            welcome-tip-pages)   install -Dm755 "$name"  "$pkgdir/$eosdir/$name" ;;
            *)                   install -Dm755 "$name"  "$pkgdir/usr/bin/$name" ;;
        esac
    done

    # create symlink: /usr/bin/eos-welcome -> /usr/share/endeavouros/scripts/welcome
    ln -s "/$eosdir/$pkgname"   "$pkgdir/usr/bin/eos-$pkgname"

    # create app desktop file /usr/share/applications/welcome.desktop
    cp -a "$pkgname.desktop" "$pkgname.desktop-enable"
    sed -i "$pkgname.desktop-enable" \
        -e 's|^\(Exec=.* --startdelay.*\)$|#\1|' \
        -e 's|^#\(Exec=.* --once.*\)$|\1|'
    install -Dm644 "$pkgname.desktop-enable"  "$pkgdir/usr/share/applications/$pkgname.desktop"      # has --once
}
