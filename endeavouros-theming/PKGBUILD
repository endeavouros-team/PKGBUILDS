# Maintainer: EndeavourOS <info@endeavouros.com>
pkgname=endeavouros-theming
pkgver=12.1
pkgrel=1
pkgdesc='Default EndeavourOS background images and icons for all different DE'
arch=('any')
replaces=('EndeavourOS-theming')
url='https://www.endeavouros.com'
license=('GPL3')
makedepends=('git')
source=("https://github.com/endeavouros-team/endeavouros-theming/archive/refs/tags/${pkgver}.tar.gz")
md5sums=('f049c1eb8fad81ba462c27e1d754554b')
sha512sums=('27ceb4aad59e13f94afcecc7ba457207bb9b1ab28917e238260e61128c3bad17d6dd5420b2d1b93e9a9f62e0ec8b1f11ec6a72a17b77afe7be102739d69e7e39')
install=$pkgname.install

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  if [ "${CARCH}" = "x86_64" ]; then
    rm -rf backgrounds-arm
  fi
  if [ "${CARCH}" = "aarch64" ]; then
    rm -rf backgrounds
    mv backgrounds-arm backgrounds
  fi
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "${pkgdir}/usr/share/endeavouros"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -d "${pkgdir}/usr/share/endeavouros/backgrounds"

  install -Dm644 *.png *.svg           "${pkgdir}/usr/share/endeavouros"
  install -Dm644 endeavouros.{svg,png} "${pkgdir}/usr/share/pixmaps"
  install -Dm644 endeavouros-icon.png  "${pkgdir}/usr/share/pixmaps"
  
  cd backgrounds
  install -Dm644 *.png                 "${pkgdir}/usr/share/endeavouros/backgrounds"

  # make symlinks for KDE:
  cd "${pkgdir}/usr/share/pixmaps"
  ln -s endeavouros.png endeavouros-logo.png
  ln -s endeavouros.svg endeavouros-logo.svg

  # cleanup
  cd $srcdir
  rm -rf ../${pkgname}
}
