# Maintainer : manuel at endeavouros.com

pkgname=iso-create-ml
pkgver=0.26.12
pkgrel=1
pkgdesc="Create a mirrorlist for the ISO"
arch=('any')
license=('GPL3')
depends=(eos-bash-shared parallel curl reflector)
source=(
    create-ml
    create-ml.bash-completion
    create-ml-rankmirrors-arch
    create-ml.conf
    create-ml-country-mapping-arch.conf
    create-ml-active-mirrors-arch.conf
)
sha256sums=('0e26aec920b0b04c2f17a56ef31f07e1c1e7e59f3bab282d9591dc164e95959b'
            '4af79c7128c1566e26951ad97e468fc5ef62588c6b76fdd8036982cdc7bc5086'
            '7036b26376843a63d3be7a5d472992c67979a1efcd7789c0316a8f4ca00324cf'
            '05fde2683a4cb5c801462405418fd0bd299a6b40cc451cf3754268e20131edb1'
            '5776615fa666b708161de831f334469c003bdb81fc95afa4ad26faa8f7675f56'
            '470cb1162ff6df54a919caaab86fa0e82c23992556f6732231c461b829314faa')

prepare() {
    local _program=${source[0]}
    cat <<EOF >&2

####################################################################################
NOTE: run $_program --update-supports to update the support files (create-ml-*.conf)
      *before* creating/releasing this package!
####################################################################################

EOF
}

package() {
    local src
    for src in "${source[@]}" ; do
        src=${src##*/}
        case "$src" in
            *.bash-completion) install -Dm644 "$src"  "$pkgdir/usr/share/bash-completion/completions/${src%.bash-completion}" ;;
            *.conf)            install -Dm644 "$src"  "$pkgdir/etc/$src" ;;
            *)                 install -Dm755 "$src"  "$pkgdir/usr/bin/$src" ;;
        esac
    done
}
