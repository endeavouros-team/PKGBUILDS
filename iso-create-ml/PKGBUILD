# Maintainer : manuel at endeavouros.com

pkgname=iso-create-ml
pkgver=0.26.8
pkgrel=1
pkgdesc="Create a mirrorlist for the ISO"
arch=('any')
license=('GPL3')
depends=(eos-bash-shared parallel curl reflector)
source=(
    create-ml
    create-ml.bash-completion
    create-ml.conf
    rankmirrors-arch
    arch-countries.conf
    arch-mirrors-active.conf
)
sha256sums=('ac34c320839f220fbb9a61dce9a4d5ac009dc1e03682613386e98100faf2269e'
            '4af79c7128c1566e26951ad97e468fc5ef62588c6b76fdd8036982cdc7bc5086'
            '6eceb8e997fb784f7205abf83a782d3ee09ef410b3b9ff26f51fe6f41af933e6'
            '7036b26376843a63d3be7a5d472992c67979a1efcd7789c0316a8f4ca00324cf'
            '5776615fa666b708161de831f334469c003bdb81fc95afa4ad26faa8f7675f56'
            'ee49d43cd0fe6bc1d8293fed5e812f5e214326782727fd64cc5ee45c1af6007d')
package() {
    # Before releasing this package run command:
    #     create-ml --update-supports

    local src
    for src in "${source[@]}" ; do
        src=${src##*/}
        case "$src" in
            *.bash-completion) install -Dm644 "$src"  "$pkgdir/usr/share/bash-completion/completions/${src%.bash-completion}" ;;
            *.conf)            install -Dm644 "$src"  "$pkgdir/etc/$src" ;;
            *)                 install -Dm755 "$src"  "$pkgdir/usr/bin/$src" ;;
        esac
    done
}
