# Original Maintainer: Gustau Castells <karasu@antergos.com>
# Maintainer: manuel at endeavouros.com
# Contributor: joekamprad <joekamprad@endeavouros.com>

pkgname=nvidia-inst
pkgver=25.4.5
pkgrel=1
pkgdesc="Script to install/uninstall nvidia driver packages in EndeavourOS"
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url=https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname
arch=('any')
license=('GPL2')
replaces=(nvidia-installer-dkms nvidia-installer-db nvidia-installer-common)
conflicts=(nvidia-installer-common)
depends=()
optdepends=(
  "linux-headers: needed by nvidia-dkms or nvidia-open-dkms"
  "linux-lts-headers: needed by nvidia-dkms or nvidia-open-dkms"
  "linux-hardened-headers: needed by nvidia-dkms or nvidia-open-dkms"
  "linux-zen-headers: needed by nvidia-dkms or nvidia-open-dkms"
  "dkms: for the dkms versions of the proprietary Nvidia drivers"
  "expac: faster way for finding available package versions"
)

source=(
  $_url/nvidia-inst
  $_url/nvidia-inst.bash-completion
  $_url/nvidia-installer-kernel-para
  $_url/nvidia-driver-supported-branches
)
sha512sums=('3b94032ee3e6011a8738fa327ab9d1d1d3f3d0753a1bf6121465e3a6a82c0b41045229e259b5ed9e79dce2521c354b7906b58853c9608f468cedf5cbb647d4d7'
            'b8857ff98870ac8cff78e31c660bde3b7308752ee4e6547682340866ee59f5801f09ede41e490380a40926536f47259f5d347fd59e8e2ffa67a8cb57f07c44e6'
            '8ee3fadd5c5c3f82d8f7c0b9273c7c970fe67486e5bcba5edae5dc7d997cd3aeb334fe8e76576b824187072c314dffcfd9cc01f2fdc46213001a6b997d720bb0'
            '429c18ecf23d56b9b996b5e48884365324b713d48cfeab09e48753cfa8afdf79516a753058f6a0b6acaaca7c6f4a690dc5816c53c5f0ad3682291f481b6e1c5d')

package() {
    local src file

    for src in "${source[@]}" ; do
        file="${src##*/}"
        case "$src" in
            *.bash-completion) install -Dm644 "$file"  "$pkgdir/usr/share/bash-completion/completions/${file%.*}" ;;
            *)                 install -Dm755 "$file"  "$pkgdir/usr/bin/$file"
        esac
    done
}
