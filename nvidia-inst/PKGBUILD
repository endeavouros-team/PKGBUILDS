# Original Maintainer: Gustau Castells <karasu@antergos.com>
# Maintainer: manuel at endeavouros.com
# Contributor: joekamprad <joekamprad@endeavouros.com>

pkgname=nvidia-inst
pkgver=26.1
pkgrel=1
pkgdesc="Script to install/uninstall nvidia driver packages in EndeavourOS"
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url=https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname
arch=('any')
license=('GPL2')
replaces=(nvidia-installer-dkms nvidia-installer-db nvidia-installer-common)
conflicts=(nvidia-installer-common)
depends=(expac)
optdepends=(
  "linux-headers: needed by nvidia-dkms or nvidia-open-dkms"
  "linux-lts-headers: needed by nvidia-dkms or nvidia-open-dkms"
  "linux-hardened-headers: needed by nvidia-dkms or nvidia-open-dkms"
  "linux-zen-headers: needed by nvidia-dkms or nvidia-open-dkms"
  "dkms: for the dkms versions of the proprietary Nvidia drivers"
)

source=(
  $_url/nvidia-inst
  $_url/nvidia-inst.bash-completion
  # $_url/nvidia-installer-kernel-para
  $_url/nvidia-driver-supported-branches
)
sha512sums=('254199536afca6ce8f96b6483074cb746efdb82eaa8e1e32f7c11d3c118c0947b8ac9737976ec2c42f0f06a20b194db8485fa1c0dbb3b90f0e00060d622eba4d'
            'b8857ff98870ac8cff78e31c660bde3b7308752ee4e6547682340866ee59f5801f09ede41e490380a40926536f47259f5d347fd59e8e2ffa67a8cb57f07c44e6'
            '1c065a3ae7ffb5daee6a73aae9b586326a8cb7b164b644626aaf64efb65593846b54e1f63469561c47a28cca2dc646efd51adfbc04504a697effcd860df6ef4c')

package() {
    local src file

    for src in "${source[@]}" ; do
        file="${src##*/}"
        case "$src" in
            *.bash-completion) install -Dm644 "$file"  "$pkgdir/usr/share/bash-completion/completions/${file%.*}" ;;
            *)                 install -Dm755 "$file"  "$pkgdir/usr/bin/$file"
        esac
    done
}
