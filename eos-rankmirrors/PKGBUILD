# Maintainer: EndeavourOS <info@endeavouros.com>

pkgname=eos-rankmirrors
pkgver=25.4
pkgrel=2
pkgdesc="EndeavourOS mirror ranking tool"
arch=('any')

# url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname"

license=(GPL3)
depends=(eos-bash-shared)
options=(!strip !emptydirs)
backup=(etc/$pkgname.conf)

source=(
  $url/$pkgname
  $url/$pkgname.completion
  $url/${pkgname}-from-hook
  $url/${pkgname}.hook
  $url/${pkgname}.conf
  $url/${pkgname}.disabled
)
sha512sums=('7ca41cf5b8c52da2785d39b278e5adb5a7ce15d48e3a80f00b1d4a3181bddd0ad31133db8d6e71195527e3fe82162d52424a557134429ec1fc5ce25f8c585151'
            'e945c073d46abf35d9cf9042c7906bf9a1bad73760e0f06fb42c59227cf0ef1e3ea9ceeb9dc3b2185b0a37711e7b698dd28565be62e21a7bddb5f3b4a2daeee4'
            'fc3113a20559ec58ffff2cacae8069ce5c83ad826f0054488315d024cb874a92ae9240fe4630bd9da647ee2cbf0e93dd22259190c703f26d21bf0b8481686a4c'
            'f01f6267f624992febfec29c76f2de1c503693086ade5783e2b9cbef359f5155bf6df520ff06299193c842aef2a05cac5078977ee0dabf55cff66f44aec6e44a'
            '8ae5bd9e5b38b172f657792b2913874304d039bdc89dabaf90545df82af8802affb0ff829b908542ba6def4174b989e163ccc78deda2c11459d10ef4dc43aee8'
            '06b77d1109fec45071dd2a3c1be07c225a5735961bd2818d0ccea608fef6e85bf4ae71d821427aa766d5b1934195282c1964965df7bd49602316b3bab6bf9824')

package() {
  local hooks=$pkgdir/usr/share/libalpm/hooks

  install -Dm644 ${pkgname}.hook      $hooks/${pkgname}.hook

  install -Dm644 ${pkgname}.conf      $pkgdir/etc/${pkgname}.conf
  install -Dm644 ${pkgname}.disabled  $pkgdir/etc/${pkgname}.disabled

  install -Dm755 $pkgname             $pkgdir/usr/bin/$pkgname
  install -Dm755 ${pkgname}-from-hook $pkgdir/usr/bin/${pkgname}-from-hook

  install -Dm644 $pkgname.completion  $pkgdir/usr/share/bash-completion/completions/$pkgname
}
