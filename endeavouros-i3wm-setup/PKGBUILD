# Maintainer: joekamprad <joekamprad@endeavouros.com>
# Contributor: The-Repo-Club <The-Repo-Club@github.com>

_pkgname=endeavouros-i3wm-setup
pkgname=endeavouros-i3wm-setup
pkgver=r134.bef442b
pkgrel=1
pkgdesc="pre user creation skel setup for i3-wm under EndeavourOS"
arch=('any')
groups=('EndeavourOS')
url="https://github.com/endeavouros-team/${pkgname}"
license=('GPL')
depends=('git')
source=("${_pkgname}::git+https://github.com/endeavouros-team/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    PREFIX=/etc/skel
    cd "$_pkgname"
    mkdir -p "${pkgdir}${PREFIX}/.config/"
    cp -R ".config/" "${pkgdir}${PREFIX}/"
    install -Dm644 ".gtkrc-2.0" "${pkgdir}${PREFIX}/.gtkrc-2.0"
    install -Dm644 ".nanorc" "${pkgdir}${PREFIX}/.nanorc"
    install -Dm644 "setup.sh" "${pkgdir}${PREFIX}/setup.sh"
    install -Dm644 "xed.dconf" "${pkgdir}${PREFIX}/xed.dconf"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
