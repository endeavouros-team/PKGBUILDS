# Maintainer: EndeavourOS-Team <info@endeavouros.com>

# Moved to eos-reboot-recommended from eos-bash-shared.

pkgname=eos-reboot-recommended
pkgdesc="Notify if a reboot is recommended after system upgrade."
pkgver=25.11
pkgrel=1
arch=('any')
license=('GPL')
depends=(
    eos-bash-shared    # notification help, detect the running kernel, detect if in chroot
    libnotify          # notifications
)

url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/refs/heads/master/$pkgname"

source=(
  $_url/eos-reboot-required2
  $_url/eos-reboot-required3
  # $_url/eos-reboot-required.conf
  $_url/eos-reboot-required.hook
  $_url/eos-reboot-required.timer
  $_url/eos-reboot-required.service
)

sha512sums=('57e8bc51b634a3a509fda5365d9a472b719fa2574a894f8e8425f8bf2b8b9b209fba04bc6b2129d56825e552e7fedeef60ec4fa2071f5ea4ebaf48dee7d29037'
            '38ccd746d1b3bad19bc7f4132e7b6193704dac4432a5ef15019bd8e619a01304155d235a307f2784095a59606a8278eebac18b5eb11bf2b344f955d42762f4bf'
            '195cc83df58e1f65b43f39202fe60aecf348e843c9c1b414897dbf6577ee7ffc67b1d1ba5ca0325e501cffebd304b6eca90f1c8fd4af4dd75b94fbc7834f59f4'
            'b26a06683f6ab8039df3befe93140a72c9e750f9c6a1e5e9b9f653a776a3ef9ab61e14a8485c53022e5959dd17eb94d8fd0be6f81a5e6a0aef95701f0100817a'
            '00557e96e9b5155424e83ae87fb242dc23968d79e63ea8831bddaf697ef5267351bc7fa2a7343b6a5c9aadaf170759e62ee1210f3dce839e395c51b00eb13d2f')

package() {
  local file src

  for src in "${source[@]}" ; do
      file="${src##*/}"
      case "$file" in
          *.conf)                                   install -Dm644 "$file"   "$pkgdir/etc/$file" ;;
          *.hook)                                   install -Dm644 "$file"   "$pkgdir/usr/share/libalpm/hooks/$file" ;;
          *.timer | *.service)                      install -Dm644 "$file"   "$pkgdir/usr/lib/systemd/system/$file" ;;
          *)                                        install -Dm755 "$file"   "$pkgdir/usr/bin/$file" ;;
      esac
  done
}
