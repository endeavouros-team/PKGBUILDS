# Maintainer: EndeavourOS-Team <info@endeavouros.com>

# Moved to eos-reboot-recommended from eos-bash-shared.

pkgname=eos-reboot-recommended
pkgdesc="Notify if a reboot is recommended after system upgrade."
pkgver=25.11.2
pkgrel=1
arch=('any')
license=('GPL')
depends=(
    eos-bash-shared    # notification help, detect the running kernel, detect if in chroot
    libnotify          # notifications
)

url="https://github.com/endeavouros-team/PKGBUILDS/raw/master/$pkgname"
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/refs/heads/master/$pkgname"

source=(
  $_url/eos-reboot-required2
  $_url/eos-reboot-required3
  # $_url/eos-reboot-required.conf
  $_url/eos-reboot-required.hook
  $_url/eos-reboot-required.timer
  $_url/eos-reboot-required.service
)

sha512sums=('29d2addcdf6122302375e40d84f6fbe06111aee49af7727dc1d1dc066f2dde4e16a221ac0d78bd93d3e427c76fa8d79a84c4f8ce2f95d64611a5798294637839'
            'b0fe0eeea5c3edfc385153ba5c8ac88ddd8f47ad251c429b18e39b8e8d5e447781c06cda5f47dcda40f82fab10747c03b338d58c43e4d8b3f32f8056a1c53d4c'
            '195cc83df58e1f65b43f39202fe60aecf348e843c9c1b414897dbf6577ee7ffc67b1d1ba5ca0325e501cffebd304b6eca90f1c8fd4af4dd75b94fbc7834f59f4'
            'b26a06683f6ab8039df3befe93140a72c9e750f9c6a1e5e9b9f653a776a3ef9ab61e14a8485c53022e5959dd17eb94d8fd0be6f81a5e6a0aef95701f0100817a'
            '00557e96e9b5155424e83ae87fb242dc23968d79e63ea8831bddaf697ef5267351bc7fa2a7343b6a5c9aadaf170759e62ee1210f3dce839e395c51b00eb13d2f')

package() {
  local file src

  for src in "${source[@]}" ; do
      file="${src##*/}"
      case "$file" in
          *.conf)                                   install -Dm644 "$file"   "$pkgdir/etc/$file" ;;
          *.hook)                                   install -Dm644 "$file"   "$pkgdir/usr/share/libalpm/hooks/$file" ;;
          *.timer | *.service)                      install -Dm644 "$file"   "$pkgdir/usr/lib/systemd/system/$file" ;;
          *)                                        install -Dm755 "$file"   "$pkgdir/usr/bin/$file" ;;
      esac
  done
}
