# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the EndeavourOS repository."
pkgver=25.10
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  eos-bash-shared
  yad
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('9866a69d85a6cf93a6b3ac4c81cb2820c5563337cd315f39706a712ec246ceb397e96caa660f79cd47a00ae9a8b098b0e37c37ec635879f063662fda4225085e'
            '264b2ce9f3981728bb45e52ab9d2ec782a5e9a291ca2b9249a5558529f4717c868545524677185cb5cc2f370f373818dd2c690fa79f1a107c34c65b3d6ea53a0'
            '53e387eb1af9693dce6cc83636de399fc8ceedbce3e237c51c84e330766caec728808a2cf65ab653289b5da7c0648f47d17009f3e3669a15ddb0eb7772bae073'
            '8a0692ff5e714a0690c7a90d04a928b2edf2e01515f8707b345d0da36ee95cab7f4f9badf3dca6cbd280438a08519957c5852be92e7936da9f0affef3d895b17'
            'f715f79cebc043ce5b74ecd55c8787d406d27ed914110aeddc79fe742b4f31cd7e3fe1be8714d4c2cbca60c636ab58f56f96d223601890cac747616ed6827fe0'
            'd2be446e59b5fd571fe3039c8dd23a8470c5c214ad6d7f4094652ae72fa60689e58c67ed50d543c1527689d840b7170c51d239a5b158898b6c92bc1b39af10c2')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
