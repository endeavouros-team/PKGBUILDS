# Maintainer: EndeavourOS-Team <info@endeavouros.com>

pkgname=eos-hooks
pkgver=1.16.2
pkgrel=1
pkgdesc='EndeavourOS pacman hooks'
arch=('any')
license=('GPL3')
url=https://github.com/endeavouros-team/PKGBUILDS/tree/master/$pkgname
_url=https://raw.githubusercontent.com/endeavouros-team/PKGBUILDS/master/$pkgname

depends=()

source=(
  $_url/25-eos-os-release.hook
  $_url/eos-lsb-release.hook
  $_url/${pkgname}.hook
  $_url/${pkgname}-runner
)

sha512sums=('d315c86c3f18278883161798b1f8ec891d20efb0bf2ada4dbf406a30767506807c494256e872cacd3c5ebe08d4f73bc96517c52d227808c5bcfbf89284094c72'
            '20568a779c4ec34e9a79559a125efb1a85aacde8474df4ab9c5f766b6eb7a6b5848c049387f78306ad844e7571cf8a22afc89e6aab18c47b5be419e3d0e4e2e7'
            '11940266adb37425e9b89b448a005a99dfc978ec0c91555cfa78f2e955a35bcd175374b775d7d31a395ce135587be106cbc8ad55eef1a9c933cea1806bf4dcfc'
            '49b50615ee1d86e4e56aefd4fa2024c880e2d4ea104b87b2c80ea86e33b436bb1d8516602e2ccc5a6317f752255843dfa1d8e27c9fb506b7a287c7e1e6b17882')

package() {
  local item
  for item in "${source[@]}" ; do
      item=${item##*/}
      case "$item" in
          *.hook)  install -Dm644 $item   $pkgdir/usr/share/libalpm/hooks/$item ;;
          *)       install -Dm755 $item   $pkgdir/usr/bin/$item ;;
      esac
  done
}
