# Maintainer : EndeavourOS <info at endeavouros.com>
# Calamares installer (fork) configured for EndeavourOS

pkgname=calamares-git
_reponame=calamares
release_name=$pkgname
pkgver=11469.97ea0e6e0
pkgrel=1
pkgdesc="Calamares installer for EndeavourOS - Development version"
arch=('any')
url="https://github.com/endeavouros-team/calamares"
license=('GPL3')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'python-jsonschema' 'python-pyaml' 'python-unidecode' 'gawk')
conflicts=('calamares_current')
depends=( 'qt6-svg' 'qt6-webengine' 'yaml-cpp' 'networkmanager' 'upower' 'kcoreaddons' 'kconfig' 'ki18n' 'kservice' \
'kwidgetsaddons' 'kpmcore' 'squashfs-tools' 'rsync' 'pybind11' 'cryptsetup' 'doxygen' 'dmidecode' \
'gptfdisk' 'hwinfo' 'kparts' 'polkit-qt6' 'python' 'solid' 'qt6-tools' 'libpwquality' 'ckbcomp' 'qt6-declarative' )
provides=("calamares")
options=(!strip !emptydirs)
source=("git+https://github.com/endeavouros-team/$_reponame.git#branch=calamares")

sha256sums=('SKIP')
            
pkgver() {
  cd "${_reponame}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    # Update branding.desc with the proper values
    replace_command='
    {
        gsub(/\${version}/,version);
        gsub(/\${release_name}/,release);
        print
    }
    '
    awk -i inplace -v version="$(pkgver)" -v release="${release_name}" "$replace_command" "${srcdir}/calamares/data/eos/branding/endeavouros/branding.desc"
}

build() {
    cmake -B build -S "${srcdir}/calamares" \
    -DWITH_QT6=ON \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_CONFIG=OFF \
    -DSKIP_MODULES="dracut \
    dummycpp dummyprocess dummypython dummypythonqt \
    finishedq initcpio keyboardq license localeq notesqml oemid \
    openrcdmcryptcfg plymouthcfg plasmalnf services-openrc \
    summaryq tracking usersq webview welcomeq"
    export DESTDIR="$srcdir/build"
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -dm 755 "${pkgdir}/etc"
    cp -rp "${srcdir}/calamares/data/eos" "${pkgdir}/etc/calamares"
}
