# Maintainer: joekamprad <joekamprad at endeavouros.com>

_pkgname=endeavouros-i3wm-setup
pkgname=endeavouros-skel-i3wm-git
pkgver=396.6d4e2bc
pkgrel=1
pkgdesc="pre user creation skel setup for i3-wm under EndeavourOS git version"
arch=('any')
groups=('EndeavourOS')
url="https://github.com/endeavouros-team/${_pkgname}"
license=('GPL')
makedepends=('git')
depends=('endeavouros-skel-default')
source=("${_pkgname}::git+https://github.com/endeavouros-team/${_pkgname}.git#branch=main")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    PREFIX=/etc/skel
    cd "$_pkgname"
    mkdir -p "${pkgdir}${PREFIX}/.config/"
    mkdir -p "${pkgdir}${PREFIX}/.config/autostart"
    cp -R ".config/dunst" "${pkgdir}${PREFIX}/.config/dunst"
    cp -R ".config/i3" "${pkgdir}${PREFIX}/.config/i3"
    chmod -R +x "${pkgdir}${PREFIX}/.config/i3/scripts"
    cp -R ".config/rofi" "${pkgdir}${PREFIX}/.config/rofi"
    cp ".config/example.picom.conf" "${pkgdir}${PREFIX}/.config/"
    cp ".profile" "${pkgdir}${PREFIX}/.profile"
}
